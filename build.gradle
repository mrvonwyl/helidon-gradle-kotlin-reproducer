plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

group 'dev.elysion.mail'
version '1.0.0-SNAPSHOT'

ext {
    helidonVersion = '1.4.3'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation platform("io.helidon:helidon-dependencies:${project.helidonVersion}")

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'io.helidon.microprofile.bundles:helidon-microprofile'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson'

    // logging
    implementation 'org.apache.logging.log4j:log4j-api:2.13.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.13.0'
    implementation 'org.apache.logging.log4j:log4j-jul:2.13.0'
    // this is needed for log4j to read yaml configs
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.10.2'

    runtimeOnly 'org.jboss:jandex'
    runtimeOnly 'javax.activation:javax.activation-api'

    testCompileOnly 'org.junit.jupiter:junit-jupiter-api'
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

// default jar configuration
// set the main classpath
// add each jar under build/libs/libs into the classpath
jar {
    archiveFileName = "${project.name}.jar"
    manifest {
        attributes('Main-Class': 'dev.elysion.mail.App',
                'Class-Path': configurations.runtimeClasspath.files.collect { "libs/$it.name" }.join(' ')
        )
    }
}

// define a custom task to copy all dependencies in the runtime classpath
// into build/libs/libs
// uses built-in Copy
task copyLibs(type: Copy) {
    from configurations.runtimeClasspath
    into 'build/libs/libs'
}

// add it as a dependency of built-in task 'assemble'
copyLibs.dependsOn jar
assemble.dependsOn copyLibs